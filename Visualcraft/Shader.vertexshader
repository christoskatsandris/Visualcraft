#version 330 core

// input vertex and UV coordinates, different for all executions of this shader
layout(location = 0) in vec3 vertexPosition_modelspace;
layout(location = 1) in vec3 vertexNormal_modelspace;
layout(location = 2) in vec2 vertexUV;
layout(location = 3) in vec3 voxelPositions;
layout(location = 4) in float heightMap;

out vec4 vertex_position_worldspace;
out vec3 vertex_position_modelspace;
out vec3 vertex_position_cameraspace;
out vec3 vertex_normal_cameraspace;
out vec2 vertex_UV;
out float heightmap;
out vec4 vertex_position_lightspace;

// model view projection matrix
uniform mat4 M;
uniform mat4 V;
uniform mat4 P;
uniform mat4 lightVP;

void main()
{
	vec4 coordinates_modelspace = vec4(vertexPosition_modelspace, 1.0);
	vertex_position_worldspace = M * coordinates_modelspace;

	vertex_position_worldspace += vec4(
		voxelPositions.x,
		-voxelPositions.y - heightMap,
		-voxelPositions.z,
		0.0
	);

	vertex_position_modelspace = vec3(vertexPosition_modelspace);
	vertex_position_cameraspace = (V * vertex_position_worldspace).xyz;
	vertex_normal_cameraspace = (V * M * vec4(vertexNormal_modelspace, 0)).xyz;
	vertex_UV = vertexUV;
	heightmap = heightMap;
	vertex_position_lightspace = lightVP * vertex_position_worldspace;
	gl_Position = P * V * vertex_position_worldspace;
}