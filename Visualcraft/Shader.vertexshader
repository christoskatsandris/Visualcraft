#version 330 core

// input vertex and UV coordinates, different for all executions of this shader
layout(location = 0) in vec3 vertexPosition_modelspace;
layout(location = 1) in vec3 vertexNormal_modelspace;
layout(location = 2) in vec2 vertexUV;
layout(location = 3) in vec3 positions;
layout(location = 4) in float heightMap;

out vec4 vertex_position_worldspace;
out vec3 vertex_position_modelspace;
out vec3 vertex_position_cameraspace;
out vec3 vertex_normal_cameraspace;
out vec2 vertex_UV;
out float heightmap;
out vec4 vertex_position_lightspace;

// model view projection matrix
uniform mat4 M;
uniform mat4 V;
uniform mat4 P;
uniform mat4 lightVP;

uniform int objectID;

void main()
{
	vertex_position_worldspace = M * vec4(vertexPosition_modelspace, 1.0);

	switch (objectID) {
		case 0: // Voxel
			vertex_position_worldspace += vec4(
				positions.x,
				-positions.y - heightMap,
				-positions.z,
				0.0
			);
			break;

		case 1: // Tree
			vertex_position_worldspace += vec4(
				positions.x - 2.0,
				-positions.y - heightMap + 1.0,
				-positions.z - 2.0,
				0.0
			);
			break;

		default: // Any other object
			vertex_position_worldspace += vec4(
				positions.x,
				-positions.y - heightMap + 1.0,
				-positions.z,
				0.0
			);
			break;
	}

	vertex_position_modelspace = vec3(vertexPosition_modelspace);
	vertex_position_cameraspace = (V * vertex_position_worldspace).xyz;
	vertex_normal_cameraspace = (V * M * vec4(vertexNormal_modelspace, 0)).xyz;
	vertex_UV = vertexUV;
	heightmap = heightMap;
	vertex_position_lightspace = lightVP * vertex_position_worldspace;
	gl_Position = P * V * vertex_position_worldspace;
}